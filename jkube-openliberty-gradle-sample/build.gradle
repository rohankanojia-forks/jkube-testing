apply plugin: 'liberty'
apply plugin: 'war'
apply plugin: 'org.eclipse.jkube.kubernetes'
apply plugin: 'org.eclipse.jkube.openshift'

version '1.0-SNAPSHOT'
group 'org.eclipse.jkube.quickstarts.gradle.openliberty'

sourceCompatibility = 11
targetCompatibility = 11
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// configure liberty-gradle-plugin
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'io.openliberty.tools:liberty-gradle-plugin:3.2'
        classpath 'org.eclipse.jkube.kubernetes:org.eclipse.jkube.kubernetes.gradle.plugin:1.5.1'
        classpath 'org.eclipse.jkube.openshift:org.eclipse.jkube.openshift.gradle.plugin:1.5.1'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // provided dependencies
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:8.0.0'
    providedCompile 'org.eclipse.microprofile:microprofile:4.1'

}

liberty {

    server {
        name = "openliberty-${version}"

        packageLiberty {
            packageName = "${name}"
            include = "runnable"
        }
    }
}

kubernetes {
    enricher {
        config {
            'jkube-service' {
                type = 'NodePort'
            }
        }
    }
}

clean.dependsOn 'libertyStop'
